class InvokeDynamic extends java/lang/Object

method <init>()V public
    aload this
    invokespecial java/lang/Object/<init>()V
    return
end

method main([Ljava/lang/String;)V public static

    invokedynamic getPrintStream ()Ljava/io/PrintStream;
     .handle H_INVOKESTATIC
        InvokeDynamic/bootstrap(Ljava/lang/invoke/MethodHandles$Lookup;Ljava/lang/String;Ljava/lang/invoke/MethodType;)Ljava/lang/invoke/CallSite;
     args end
    ldc "Hello, World!"
    invokevirtual java/io/PrintStream/println(Ljava/lang/String;)V

    return

end

method bootstrap(Ljava/lang/invoke/MethodHandles$Lookup;Ljava/lang/String;Ljava/lang/invoke/MethodType;)Ljava/lang/invoke/CallSite; public static

    new java/lang/invoke/ConstantCallSite # create a new CallSite
    dup

    aload handle
    ldc .type java/lang/System
    ldc "out"
    ldc .type java/io/PrintStream
    invokevirtual java/lang/invoke/MethodHandles$Lookup/findStaticGetter(Ljava/lang/Class;Ljava/lang/String;Ljava/lang/Class;)Ljava/lang/invoke/MethodHandle;

    invokespecial java/lang/invoke/ConstantCallSite/<init>(Ljava/lang/invoke/MethodHandle;)V

    areturn

end